/* format: See `/docs/language/ebnf.md`. */


lsd ::= LsdVersionHeader
        DocumentIdentifierHeader
        DocumentVersionHeader
        body


/* Header */

LsdVersionHeader ::= "lsd/version: 0.1.0" NL+
DocumentIdentifierHeader ::= "document/identifier:" SPACE Label NL+
DocumentVersionHeader ::= "document/version:" SPACE VersionString NL+

/* See <https://semver.org/>. */
VersionString ::= Number "." Number "." Number PrereleaseVersionSuffix?
PrereleaseVersionSuffix ::=
  "-" PrereleaseVersionSegment ("." PrereleaseVersionSegment)*
PrereleaseVersionSegment ::= Number | Digit* (Letter|"-") (Digit|Letter|"-")*


/* Basic tokens */

Label ::= Letter ((Digit|Letter|"-"|"_")* (Digit|Letter))?

Number ::= 0 | [1-9] Digit*
Digit ::= [0-9]
Letter ::= [a-zA-Z]
Symbol ::= /* Unicode character classes P* (Punctuation) and S* (Symbol) */

NL ::= #xA

SPACE ::= #x20

/* Not to be confused with `INDENT`. */
INDENTATION ::= SPACE SPACE


/* Secondary types */

NodeKeyword ::= Symbol | "[" Label "]"

Bool ::= "true"|"false"


/* Body */

body ::= typeDef+

/* See `./type-constructors.md`. */
typeDef ::= "type/" KindName SPACE TypeConstructorName TypeName NL+
            (INDENTATION AttrName ":" SPACE attrValue NL+)*
  [vc: The named type constructor for the given kind must exist.]
  [vc: The list of attributes and their values must match what the given type
       constructor expects.]
  [vc: If an attribute value refers to types, these must be defined.]

KindName ::= Label
TypeConstructorName ::= Label
TypeName ::= Label


/* vi: ts=2 sw=0 et */
